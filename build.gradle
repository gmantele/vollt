subprojects {
    apply plugin: 'java-library'
    apply plugin: 'java-library-distribution'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    compileJava {
        options.encoding = "UTF-8"
    }

    test{
        forkEvery = 1
        include '**/Test*'
    }

    tasks.register('srcZip', Zip) {
        group = 'build'
        description = 'Assembles all source and test files into a ZIP file.'

        from projectDir
        include 'src/**/*'
        include 'test/**/*'
        include '*.gradle'
        include 'README.md'
        include 'COPYING'
        include 'COPYING.LESSER'
        doLast {
            println "Path to zip: "+  archiveFile.get().asFile.path
        }
    }
    assemble.dependsOn(srcZip)

    sourceSets.main.java.srcDirs = ["src"]
    sourceSets.test.java.srcDirs = ["test"]

    /* Set the minimum Java version compatibility: */
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    dependencies {
        testImplementation 'junit:junit:4.13.1'
        testImplementation 'org.slf4j:slf4j-simple:1.7.25'
    }
}